# Maintainer: Jordan Johnston <johnstonljordan at gmail dot com>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=fsthost
pkgver=1.6.2
pkgrel=3
pkgdesc='A linux VST host using winelib'
arch=('i686' 'x86_64')
url='http://sourceforge.net/projects/fsthost/'
license=('GPL')
depends=('jack' 'libxml2')

_GTK=3 # Use gtk version 3 as default, 0 mean
[[ $_GTK > 0 ]] && \
depends+=("gtk${_GTK}")

if [[ $CARCH = 'x86_64' ]]; then
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig
  _arch=32
  depends+=(${depends[@]/#/lib32-})
fi

depends+=('wine' 'perl-xml-libxml')

source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.xz"
        win32_prio_corrected.patch
        wine-NSPA_env.patch)
md5sums=('0769970ea13f3e6b860944db3803d339'
         '6c35534fbd24bde575060440e2f00e65'
         '4fc6d9e27dfbd8d40f420f0bd2fd51d9')

build() {
  cd $pkgname-$pkgver
  
  patch -Np1 -i $srcdir/win32_prio_corrected.patch
  patch -Np1 -i $srcdir/wine-NSPA_env.patch
    
  make LIB32_INST_PATH=/usr/lib$_arch \
       LIB64_INST_PATH=/usr/lib \
       LASH_EXISTS='no' \
       GTK=${_GTK}
}

package() {
  cd $pkgname-$pkgver
  make LIB32_INST_PATH=/usr/lib$_arch \
       LIB64_INST_PATH=/usr/lib \
       MANDIR=/usr/share/man/man1 \
       DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
