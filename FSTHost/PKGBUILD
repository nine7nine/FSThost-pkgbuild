# Maintainer: Jordan Johnston <johnstonljordan at gmail dot com>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=FSTHost
pkgver=1.6.3
pkgrel=1
pkgdesc='A linux VST host using winelib'
arch=('i686' 'x86_64')
url='http://sourceforge.net/projects/fsthost/'
license=('GPL')
depends=('jack' 'libxml2')

_GTK=3 # Use gtk version 3 as default, 0 mean
[[ $_GTK > 0 ]] && \
depends+=("gtk${_GTK}")

if [[ $CARCH = 'x86_64' ]]; then
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig
  _arch=32
  depends+=(${depends[@]/#/lib32-})
fi

depends+=('wine' 'perl-xml-libxml')

source=("git+https://github.com/nine7nine/FSTHost.git"
        win32_prio_corrected.patch
        jamc_silence_logging.patch
        wine-NSPA_env.patch)
md5sums=('SKIP'
         'f62350e9032c39c952ba036f690c43ec'
         '1e5ca84ece8bc67ed0ddd522cf79e4a5'
         'e5bf8bf56a4fdc9b27521b473e23c7da')

build() {
  cd $pkgname
  
  patch -Np1 -i $srcdir/win32_prio_corrected.patch
  patch -Np1 -i $srcdir/wine-NSPA_env.patch
  patch -Np1 -i $srcdir/jamc_silence_logging.patch
     
  make LIB32_INST_PATH=/usr/lib$_arch \
       LIB64_INST_PATH=/usr/lib \
       LASH_EXISTS='no' \
       GTK=0
}

package() {
  cd $pkgname
  make LIB32_INST_PATH=/usr/lib$_arch \
       LIB64_INST_PATH=/usr/lib \
       MANDIR=/usr/share/man/man1 \
       DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
